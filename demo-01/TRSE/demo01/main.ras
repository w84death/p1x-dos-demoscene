program demo01;

@use "system/screen"
@use "system/memory"
@use "system/file"
@use "gfx/gfx"	
@use "system/tables"
@use "gfx/fonts"
@use "music/beeplay"
@use "system/compression"
@use "math/random"



@export "music/tune.trt" "music.bin" 3
@export "fonts/font.flf" "fonts/font.bin" 255
@compress "fonts/font.bin" "font.bin"
@export "sprites/sprites.flf" "sprites/sprites.bin" 255
@compress "sprites/sprites.bin" "sprites.bin"	
@export "sprites/logos.flf" "sprites/logos.bin" 255
@compress "sprites/logos.bin" "logos.bin"

var	
	song: incbin("music.bin");
	font: incbin("font.bin");
	sprites: incbin("sprites.bin");
	logos: incbin("logos.bin");
	
	i,j,time,scene_time : integer = 0;
	color,color2,c : integer = 0;
	current_scene : integer = 3;
	current_scene_len : integer;
	last_scene : integer = 3;
	debug : byte = 0;
	p : pointer;
		
	/*
	 * SCENES
	 */
	particle_type = record
			x,y,z:signed integer;
			speed:integer;
	end;
	stars : array[300] of particle_type;
	stars_colors : array[256] of byte = buildtable("Math.sin(i*Math.PI/512)*63");

	sprite_type = record
		x,y,w,h: integer;
	end;
	
	spr_ship_anim : array[9] of sprite_type;
	spr_kkj_anim : array[9] of sprite_type;
	spr_ship_big : array[4] of sprite_type;
	spr_space_props : array[12] of sprite_type;
	spr_logos : array[4] of sprite_type;
	
	skyline_distribution : array[256] of integer = buildtable("4+Math.sin(i*Math.PI/256)*32*Math.random()");
	skyline_sizes : array[8] of integer = buildtable("8+32-Math.sin(Math.random())*32");
	depth_color : array[128] of integer = buildtable("63-Math.sin(i*Math.PI/512)*63");

	factor : integer;
	power_effect:integer=100;
	scene_intro_bar_size:byte=0;
	last_spr : byte=0;
	/* 
    * SCENES
	 */
	x1,x2 : integer = 160;
	y1,y2 : integer = 100;
	x,y,z : integer;
	size,w,h : integer;
	sx,sy,sz : signed integer;
	shift : integer = 0;
	const line_width : byte = 15;
	
	sinx : array[256] of integer = buildsinetable(320-line_width);
	siny : array[256] of integer = buildsinetable(200-line_width);
	sin64 : array[128] of byte = buildsinetable(64);
	sin32 : array[256] of byte = buildsinetable(32);
	sin256 : array[256] of byte = buildsinetable(256);
	
	scene_intro_color : byte = 0;
	
	/*
	 * STRINGS
	 */
	demo_name_text: string	= " C:/>DEMO1.EXE ";
	powered_by_text: string	= "POWERED BY TRSE";
	intel486_text: string	= " FOR INTEL 486 ";
	outro_text: string="* SCENE.P1X.IN *";	
	date_text: string="OCTOBER 2023";
	tp:pointer;

procedure SetPalette(pal : integer);
begin
	case pal of
		0: begin						
			for i:=0 to 64 do
			begin	
				c:=32+sin64[i];
				Screen::SetColor(i,		c/2,c/4,63-c);
				Screen::SetColor(64+i,	4+i/2,4+i/2,4+i/2);
				Screen::SetColor(128+i,	i/2,i/3,i);
			end;
			
			Screen::SetColor(0,		4,4,4);
			for i:=1 to 16 do
			begin	
				Screen::SetColor(i,		8+i*2,8+i*4,8+i*2.5);
			end;
		end;
		
		1: begin	
			for i:=0 to 64 do
			begin	
				c:=32+sin64[i];
				Screen::SetColor(i,		c/2,c/4,63-c);
				Screen::SetColor(64+i,	4+i/2,4+i/2,4+i/2);
				Screen::SetColor(128+i,	i/2,i/3,i);
			end;
			Screen::SetColor(0,		4,4,4);
			
			for i:=1 to 16 do
			begin	
				Screen::SetColor(i,		8+i*2.5,8+i*2,8+i*4);				
				Screen::SetColor(i+16,	8+i*2.5,8+i*1,8+i*1);
			end;
		end;
		
		2: begin						
			for i:=0 to 64 do
			begin
				c:=32+sin64[i];	
				Screen::SetColor(i,		2+i/2,2+i/2,2+i/2);
				Screen::SetColor(64+i,	i/4,i/2,i/4);
				Screen::SetColor(128+i,	i/4,0,16-i/8);
				Screen::SetColor(192+i,	i/2,i/2,0);
			end;
		end;
		
		3: begin						
			for i:=0 to 64 do
			begin
				c:=32+sin64[i];	
				Screen::SetColor(i,		4+i*2,8+i*3,16+i*4);
				Screen::SetColor(64+i,	i/4,i/2,i/4);
				Screen::SetColor(128+i,	i/4,0,16-i/8);
				Screen::SetColor(192+i,	i/2,i/2,0);
			end;
			Screen::SetColor(0,		2,2,2);
		end;
	end;
end;

procedure DrawPalette();
begin
	for i:=0 to 256 do
	begin
		Gfx::DrawLine(i,0,i,8,i,Screen::buffer1);
	end;	
end;

procedure VFXSpriteDraw(x1,y1,w,h,x2,y2:global integer;bank:pointer);
begin
	for i:=0 to h do
	begin
		Memory::CopyData16(bank+((y1+i)*320+x1),Screen::buffer1+((y2+i)*320+x2),w/2);
	end;
end;

procedure P1XLogoDraw(logo_color:byte);
begin
	x:=120;
	y:=40;
	
	Gfx::DrawSquare(x,y,x+5,y+100,logo_color,Screen::buffer1);
	Gfx::DrawSquare(x+5,y,x+25,y+5,logo_color,Screen::buffer1);
	Gfx::DrawSquare(x+5,y+55,x+25,y+60,logo_color,Screen::buffer1);
	Gfx::DrawSquare(x+25,y,x+30,y+60,logo_color,Screen::buffer1);
	
	Gfx::DrawSquare(x+40,y,x+45,y+100,logo_color,Screen::buffer1);
	Gfx::DrawSquare(x+35,y+5,x+40,y+10,logo_color,Screen::buffer1);
	
	Gfx::DrawSquare(x+55,y,x+60,y+40,logo_color,Screen::buffer1);
	Gfx::DrawSquare(x+75,y,x+80,y+40,logo_color,Screen::buffer1);
	Gfx::DrawSquare(x+55,y+55,x+60,y+100,logo_color,Screen::buffer1);
	Gfx::DrawSquare(x+75,y+55,x+80,y+100,logo_color,Screen::buffer1);
	
	Gfx::DrawSquare(x+60,y+40,x+65,y+45,logo_color,Screen::buffer1);
	Gfx::DrawSquare(x+60,y+50,x+65,y+55,logo_color,Screen::buffer1);
	Gfx::DrawSquare(x+70,y+40,x+75,y+45,logo_color,Screen::buffer1);
	Gfx::DrawSquare(x+70,y+50,x+75,y+55,logo_color,Screen::buffer1);
	Gfx::DrawSquare(x+65,y+45,x+70,y+50,logo_color,Screen::buffer1);
end;

procedure VfxRenderStars();
begin
	for i:=0 to 300 do
	begin
		stars[i].z -= stars[i].speed;
		if(stars[i].z<=0) then stars[i].z:=255;
		
		sx:=stars[i].x+5000;
		sy:=stars[i].y+5000;
		sz:=stars[i].z;
		x:=(sx/sz)+160;
		y:=(sy/sz)+100;		
		
		color:=127-stars_colors[sz];

		if (x>0 and (x<320 and (y>0 and y<200))) then
		begin
			Gfx::PutPixelVGA(Screen::buffer1,x,y,color);
		end		
	end;
end;

@include "scenes/intro.ras"
@include "scenes/space.ras"
@include "scenes/highway.ras"
@include "scenes/outro.ras"

procedure ResetScene(scene_to_reset: integer);
begin
	Screen::ClearBuffer32bit(Screen::buffer1,0);
	case scene_to_reset of
		0: begin
			SceneIntroReset();
			end;
		1: begin
			SceneSpaceReset();
			end;			
		2: begin
			SceneHighwayReset();
			end;
		3: begin
			SceneOutroReset();
			end;
	end;
end;

procedure RenderScene(scene_to_render: integer);
begin
	case scene_to_render of
		0: begin
			Screen::ClearBuffer32bit(Screen::buffer1,0);
			SceneIntroRender();
			end;
		1: begin
			Screen::ClearBuffer32bit(Screen::buffer1,0);
			SceneSpaceRender();
			end;
		2: begin
			SceneHighwayRender();
			end;
		3: begin
			Screen::ClearBuffer32bit(Screen::buffer1,0);
			SceneOutroRender();
		end;
	end;
end;


interrupt RasterIRQ();
begin
	StartIRQ();
	BeePlay::Update();
	BeePlay::Play();
	inc(time);
	inc(scene_time);
	CloseIRQ();
end;

@include "sprites.ras"

begin
	Memory::Init();
	Screen::SetMode($13);
	Screen::InitVGABuffers(5);	
	
	/*
	 * LOAD TUNE
	 */


	BeePlay::Initialize(#song);
	BeePlay::decayLength[0]:=4; // Sustain of columm 0
	BeePlay::decayLength[1]:=6; // Sustain of column 1
	BeePlay::speed := 3;
	BeePlay::octShift:=1;

	Memory::SetTimer(33860);
	SetInterrupt($8, RasterIRQ());
	
	/*
	 * LOAD FONT
	 */
	Screen::CopyVGABuffer32bit(#font,Screen::buffer5);
	Compression::Decompress(Screen::buffer5, Screen::buffer3);
	Fonts::Init(Screen::buffer3,20,24,16,Fonts::DEFAULT_SHIFT);

	/*
	 * LOAD SPRITES
	 */
	Screen::CopyVGABuffer32bit(#sprites,Screen::buffer5);
	Compression::Decompress(Screen::buffer5, Screen::buffer2);
	
	Screen::CopyVGABuffer32bit(#logos,Screen::buffer5);
	Compression::Decompress(Screen::buffer5, Screen::buffer4);

	MapSprites();
	
	/*
	 * INIT FIRST SCENE
	 */
	ResetScene(current_scene);
	SetPalette(current_scene);	
	
	/*
	 * DEMO LOOP
	 */
	while (true) do
	begin
		waitforverticalblank();
		Screen::CopyVGABuffer32bit(Screen::buffer1,ptr(Screen::VGA,0));
		
		if (scene_time>current_scene_len) then
		begin
			current_scene+=1;
			scene_time:=0;
			if (current_scene>last_scene) then
			begin
				current_scene:=0;
				time:=0;
			end;
			
			ResetScene(current_scene);
			SetPalette(current_scene);
		end;
		
		RenderScene(current_scene);
		
		if(debug=1) then DrawPalette();
	end;
	
end.